/////////////////////////////////////////////////////////////////////////////
//                                                                            
//  File Name:    Setup.rul                                                   
//                                                                            
//  Description:  InstallShield script                                          
//                                                                            
//  Comments:     This minimal project is intended for advanced users. 
//                To create a robust project with step-by-step 
//				  instructions, use the Project Wizard. 
//                                                                                                               
/////////////////////////////////////////////////////////////////////////////

// Include header files /////////////////////////////////////////////////////
#include "ifx.h"

////////////////////// string defines ////////////////////////////
    #define UCLID_DIR "\\UCLID Software\\CommonComponents"
    #define EXTRACT_DIR "\\Extract Systems\\CommonComponents"
    #define REG_PATH_SQL_NATIVE_CLIENT "SOFTWARE\\Microsoft\\Microsoft SQL Native Client 10.0\\CurrentVersion"
    #define REG_PATH_SQL_COMPACT_EDITION "SOFTWARE\\Microsoft\\Microsoft SQL Server Compact Edition\\v3.5"
    #define SQL_COMPACT_EDITION_GUID "{E59113EB-0285-4BFD-A37A-B79EAC6B8F4B}"
    #define REG_PATH_DOT_NET_VER_2_0 "\\SOFTWARE\\Microsoft\\.NETFramework\\policy\\v2.0"
    #define REG_PATH_UNINSTALL "\\SOFTWARE\\Microsoft\\Windows\\CurrentVersion\\Uninstall"
    #define FLEX_INDEX_PRODUCT_CODE "{A7DFE34D-A07E-4D57-A624-B758E42A69D4}"
    #define IDSHIELD_PRODUCT_CODE "{158160CD-7B55-462F-8477-7E18B2937D40}"  
    #define LASERFICHE_PRODUCT_CODE "{74E1DF54-C96E-479F-915D-A0A721F9AC8B}"
    #define IDSHIELDOFFICE_PRODUCT_CODE "{A8DDFDC1-069D-42DE-AF69-A78FC232A86A}"
    #define EXTRACT_LM_PRODUCT_CODE "{EB8DE231-8B66-4DE6-A56D-39452D8CF35F}"
    #define LABDE_PRODUCT_CODE "{0E412937-E4FA-4737-A321-00AED69497C7}"
    #define LOG_FILE_PATH CommonAppDataFolder ^ "\\Extract Systems\\LogFiles\\Misc"
    #define CRYSTAL_REPORTS_GUID "{C484CC8D-03CF-4022-89C4-DB4F02E8A15B}"
    #define INLITE_INSTALLER_EXE "ClearImageEUK_5_7_3.exe"
    #define EXCEPTION_FILE_NAME "ExtractException.uex"
    #define UCLID_EXCEPTION_FILE_NAME "UCLIDException.uex"
    #define SQL_CE_DATABASE_VIEWER_AND_EDITOR_CODE "{0EACBFA9-5D4F-4D7E-8FB2-5C9E66FB6632}"
    #define REG_PATH_DOT_NET_40_INSTALL "\\SOFTWARE\\Microsoft\\NET Framework Setup\\NDP\\v4\\Full"  
    #define INSTALL_UTIL_PATH "C:\\WINDOWS\\Microsoft.NET\\Framework\\v4.0.30319\\InstallUtil.exe"
    
//////////////////// installation declarations ///////////////////

    // In order to have your InstallScript function executed as a custom
    // action by the Windows Installer, it must be prototyped as an 
    // entry-point function.

    // The keyword export identifies MyFunction() as an entry-point function.
    // The argument it accepts must be a handle to the Installer database.
    
      /* export prototype MyFunction(HWND); */  

    // To Do:  Declare global variables, define constants, and prototype user-
    //         defined and DLL functions here.


// ----- DLL function prototypes -----


    // your DLL function prototypes


// ---- script function prototypes -----


    // your script function prototypes    
    prototype MoveRegKeys();
    prototype CopyLicFiles();
	prototype InstallSQLNativeClient();     
	prototype detectRDTInstall();
	prototype InstallDotNET();
    prototype CheckOtherExtractProducts();
    prototype RenameUEXFile();
    prototype InstallInliteKeyDrivers();
    prototype UninstallInliteKeyDrivers();
    prototype InstallCrystalReports();
    prototype CloseApps();
    prototype UninstallCrystalReports12();
    prototype BOOL CheckProductKeyInstalled(STRING, STRING, BYREF STRING, BOOL);
    prototype InstallSQLCE();
    prototype DetectDotNET_40();
    prototype UninstallInstallSQLCE();
    prototype InstallDotNetService(STRING);
    prototype UninstallDotNetService(STRING);    
	prototype BOOL AreOtherExtractProductsInstalled();
	prototype InstallOfficeRedist();

    // your global variables
    BOOL gbInstalled;



///////////////////////////////////////////////////////////////////////////////
//
// Function: MoveRegKeys
//
//  Purpose: This function moves registry keys and values from the old UCLID Software keys
//			 to the Extract Systems keys
//	Returns 0 if no errors
///////////////////////////////////////////////////////////////////////////////
function MoveRegKeys()
begin
	// Move any remaining old keys to the new key path
	// Move the keys for Local machine to new Extract Systems settings
	RegDBSetDefaultRoot(HKEY_LOCAL_MACHINE);
	if ( RegDBKeyExist ("Software\\UCLID Software" ) > 0 )  then
		RegDBCopyKeys("Software\\UCLID Software",
					  "Software\\Extract Systems",
					  HKEY_LOCAL_MACHINE,
					  HKEY_LOCAL_MACHINE );
		RegDBDeleteKey("Software\\UCLID Software");
	endif;
	
	// Move the keys for current user to new Extract Systems settings
	RegDBSetDefaultRoot(HKEY_CURRENT_USER);
	if ( RegDBKeyExist ("Software\\UCLID Software" ) > 0 )  then
		RegDBCopyKeys("Software\\UCLID Software",
					  "Software\\Extract Systems",
					  HKEY_CURRENT_USER,
					  HKEY_CURRENT_USER );
		RegDBDeleteKey("Software\\UCLID Software");
	endif;
end; 

///////////////////////////////////////////////////////////////////////////////
//
// Function: CopyLicFiles
//
//  Purpose: This function copies the .lic files from the C:\Program Files\UCLID Software\CommonComponents directory
// 			 to the c:\Program Files\Extract Systems\CommonComponents directory
//	Returns 0 if no errors
///////////////////////////////////////////////////////////////////////////////
function CopyLicFiles()
	STRING strSource;
begin
	strSource  = PROGRAMFILES + UCLID_DIR;
	if ( Is( PATH_EXISTS, strSource )) then
		SRCDIR = strSource;
		TARGETDIR = PROGRAMFILES + EXTRACT_DIR;
		// Copy lic file from old dir to new	
		XCopyFile( "*.lic", "", EXCLUDE_SUBDIR | COMP_UPDATE_DATE);
	endif;
end;
// --- include script file section ---


///////////////////////////////////////////////////////////////////////////////
//
// Function: InstallSQLNativeClient
//
//  Purpose: This function installs the SQL Native Client if it is not instaled
//	Returns 0 if no errors
///////////////////////////////////////////////////////////////////////////////
function InstallSQLNativeClient()
STRING szProgram, szCmdLine;
int nResult, svServiceState;
begin
	// Change the root for the registry
	RegDBSetDefaultRoot(HKEY_LOCAL_MACHINE);
	//if ( RegDBKeyExist ( REG_PATH_SQL_NATIVE_CLIENT ) < 0) then
		// Install SQL Native Client
		szProgram = "msiexec";
		if ( SYSINFO.bIsWow64 ) then
			szCmdLine = " /i " + SUPPORTDIR ^ "\\sqlncli_x64.msi /passive";
		else
			szCmdLine = " /i " + SUPPORTDIR ^ "\\sqlncli.msi /passive";
		endif;
		LaunchAppAndWait(szProgram, szCmdLine, LAAW_OPTION_WAIT); 
		SdShowMsg("Setup is Installing SQL Native Client", FALSE);
		//if ( RegDBKeyExist ( REG_PATH_SQL_NATIVE_CLIENT ) < 0  ) then
		//	MessageBox("SQL Native Client was not successfully Installed, installation will be aborted", SEVERE);
		//	gbInstalled = FALSE;
		//	abort;
		//endif;
	//endif;

end;


///////////////////////////////////////////////////////////////////////////////
//
//  FUNCTION:   detectRDTInstall
//
//  EVENT:      Check for installed RDT
//
///////////////////////////////////////////////////////////////////////////////
function detectRDTInstall()
	STRING strRDTUninstallKey;
	NUMBER nSize, nType;
begin     
	RegDBSetDefaultRoot(HKEY_LOCAL_MACHINE);
	// check uninstall key in registry     
	strRDTUninstallKey = REG_PATH_UNINSTALL ^ "\\InstallShield_{735E1622-3990-445F-9E5D-B0D7FDE292A3}";
	
	if (RegDBKeyExist(strRDTUninstallKey) = 1 ) then
		// this means FLEX Index RDT is installed the uninstall can not continue
		MessageBox("RDT must be uninstalled before " + @PRODUCT_NAME + " can be removed", SEVERE);
		gbInstalled = FALSE;
		abort;
	endif;	
	
	return 0;
end;	
            
            
///////////////////////////////////////////////////////////////////////////////
//
//  FUNCTION:   InstallDotNET
//
//  EVENT:      Installs .NET 2.0 if it is not installed
//
///////////////////////////////////////////////////////////////////////////////
function InstallDotNET()
STRING szProgram;
int nResult, svServiceState;
begin
	// Change the root for the registry
	//RegDBSetDefaultRoot(HKEY_LOCAL_MACHINE);
	//if ( RegDBKeyExist ( REG_PATH_DOT_NET_35_INSTALL ) < 0) then
	//	szProgram = SUPPORTDIR ^ "\\dotnetfx35.exe";
	//	LaunchAppAndWait(szProgram, "", LAAW_OPTION_WAIT); 
	//	SdShowMsg("Setup is Installing .NET Framework 3.5", FALSE);
	//	if ( RegDBKeyExist ( REG_PATH_DOT_NET_35_UNINSTALL ) < 0  ) then
	//		MessageBox(".NET Framework was not successfully Installed, installation will be aborted", SEVERE);
	//		gbInstalled = FALSE;
	//		abort;
	//	endif;
	//endif;
    DetectDotNET_40();
end;

///////////////////////////////////////////////////////////////////////////////
//
//  FUNCTION:   CheckProductKeyInstalled
//
//  EVENT:      Checks for install of a product
//				if bCheckForSame returns true if same version is installed
//				if different version or none returns false
//				if bCheckForSame is false returns true if different version is installed
//				and false if same or not installed.
//
///////////////////////////////////////////////////////////////////////////////
function BOOL CheckProductKeyInstalled(strExpectedVersion, strProductKey, svInstalledVersion, bCheckForSame)
	STRING strUninstallKey;
	STRING strVersion, strEnvVersion, strRtn;
	NUMBER nSize, nType;
	STRING strMsg;
	STRING strProductName;
	NUMBER nPos; 
	NUMBER i;
begin 
	RegDBSetDefaultRoot(HKEY_LOCAL_MACHINE);

	// Get the Expected Version	                                          
	nPos = StrFind(strExpectedVersion, "Ver. ");
	StrSub(strVersion, strExpectedVersion, nPos + 5 , 20 );
	  
	// Create the path for the uninstall key
	strUninstallKey = REG_PATH_UNINSTALL ^ strProductKey;
	                                        
	// If key exists get the version
	if (RegDBKeyExist(strUninstallKey) = 1 ) then
		// Read the key if it does not match the version being installed exit
		RegDBGetKeyValueEx( strUninstallKey, "DisplayVersion", nType, svInstalledVersion, nSize); 

		if (bCheckForSame) then   
			if ( strVersion = svInstalledVersion ) then
				return TRUE;                            
			endif;
		else
			return strVersion != svInstalledVersion;
		endif;
	endif;
	                    
	// No previous version was found
	return FALSE;	
end;
            
///////////////////////////////////////////////////////////////////////////////
//
//  FUNCTION:   CheckOtherExtractProducts
//
//  EVENT:      Checks for install a different version
//				than the version being installed.
//
///////////////////////////////////////////////////////////////////////////////
function CheckOtherExtractProducts()
	STRING strUninstallKey;
	STRING strVersion;
	NUMBER nSize, nType;
	STRING strMsg;
	STRING strProductName;
	BOOL bInstalled;
begin
	bInstalled = FALSE;     
	RegDBSetDefaultRoot(HKEY_LOCAL_MACHINE);
 	
    // Check each of the expected version against any installed versions
    if (CheckProductKeyInstalled(@FlexIndexVersion, IDSHIELD_PRODUCT_CODE, strVersion, FALSE)) then
    	strMsg = "ID Shield version " + strVersion + " must be uninstalled before product can be installed.";	
    	bInstalled = TRUE;
	elseif (CheckProductKeyInstalled(@FlexIndexVersion, FLEX_INDEX_PRODUCT_CODE, strVersion, FALSE)) then
    	strMsg = "FLEX Index version " + strVersion + " must be uninstalled before product can be installed.";	
    	bInstalled = TRUE;
	elseif (CheckProductKeyInstalled(@ReusableComponentsVersion, EXTRACT_LM_PRODUCT_CODE, strVersion, FALSE)) then 
		strMsg = "Extract LM version " + strVersion + " must be uninstalled before product can be installed.";	
    	bInstalled = TRUE;
	elseif (CheckProductKeyInstalled(@LaserficheVersion, LASERFICHE_PRODUCT_CODE, strVersion, FALSE)) then 
		strMsg = "ID Shield for Laserfiche version " + strVersion + " must be uninstalled before product can be installed.";	
    	bInstalled = TRUE;
	elseif (CheckProductKeyInstalled(@IDShieldOfficeVersion, IDSHIELDOFFICE_PRODUCT_CODE, strVersion, FALSE)) then 
		strMsg = "ID Shield Office version " + strVersion + " must be uninstalled before product can be installed.";	
    	bInstalled = TRUE;
   	elseif (CheckProductKeyInstalled(@LabDEVersion, LABDE_PRODUCT_CODE, strVersion, FALSE)) then 
		strMsg = "LabDE version " + strVersion + " must be uninstalled before product can be installed.";	
    	bInstalled = TRUE;
	endif;    	
    
    // If one of the products was installed display dialog and terminate install
	if ( bInstalled ) then
		MessageBox(strMsg, SEVERE);
		gbInstalled = FALSE;
		abort;
	endif;	

	return 0;
end;	

///////////////////////////////////////////////////////////////////////////////
//
// Function: RenameUEXFile
//
//  Purpose: This function renames exception file if it exist to
//			 "YYYY-MM-DD HHhMMmSSs <EXCEPTION_FILE_NAME>" where the YYYY/MM/DD/etc 
//			 are the year/month/day and other time related fields
//	Returns 0 if no errors
///////////////////////////////////////////////////////////////////////////////
function RenameUEXFile()
	STRING strSource;
	STRING strDest;
	NUMBER nvResult;
	STRING strDate;
	STRING strTime;
	LIST listDateID;
	LIST listTimeID;
	STRING strMonth, strDay, strYear;
	STRING strHour, strMinute, strSeconds;
begin
	strSource  = LOG_FILE_PATH ^ EXCEPTION_FILE_NAME;
	
	// Check if the exception file exists
	if ( Is( FILE_EXISTS, strSource )) then  
		// Get the date
		GetSystemInfo ( DATE , nvResult , strDate );
		
		// Separate date into Month, Day, year
		listDateID = ListCreate(STRINGLIST);
		StrGetTokens ( listDateID , strDate , "-" );
		ListGetFirstString ( listDateID, strMonth );
		ListGetNextString ( listDateID, strDay );
		ListGetNextString ( listDateID, strYear );

		// Get the time
		GetSystemInfo ( TIME , nvResult , strTime );  
		
		// Separate the Time into Hours, Minutes, Seconds
		listTimeID = ListCreate(STRINGLIST);
		StrGetTokens ( listTimeID , strTime , ":" );
		ListGetFirstString ( listTimeID, strHour );
		ListGetNextString ( listTimeID, strMinute );
		ListGetNextString ( listTimeID, strSeconds );
		  
		// Add the leading 0 if the month is only 1 digit
		if (StrLengthChars(strMonth) = 1) then
			strMonth = '0' + strMonth;
		endif;
		                                                 
		// Add the leading 0 if the day is only 1 digit
		if (StrLengthChars(strDay) = 1) then
			strDay = '0' + strDay;
		endif;
			
		// Create the new filename for the exception file
		strDest = LOG_FILE_PATH ^ "\\";
		strDest = strDest + strYear + "-" + strMonth + "-" + strDay;
		strDest = strDest + " " + strHour + "h" + strMinute + "m" + strSeconds + "s ";
		strDest = strDest + EXCEPTION_FILE_NAME;
		
		// Rename exception file
		RenameFile ( strSource , strDest );
	endif;
end;

///////////////////////////////////////////////////////////////////////////////
//
// Function: InstallInliteKeyDrivers
//
//  Purpose: This function installs the drivers for the Inlite Key
///////////////////////////////////////////////////////////////////////////////
function InstallInliteKeyDrivers()
	string szProgram;
begin  
	szProgram = SUPPORTDIR ^ "\\CBUSetup.exe";
	LaunchAppAndWait(szProgram, "/Q /CRYPTOKEN", LAAW_OPTION_WAIT | LAAW_OPTION_SHOW_HOURGLASS);
	
	szProgram = SUPPORTDIR ^ INLITE_INSTALLER_EXE;
	LaunchAppAndWait(szProgram, "/s ", LAAW_OPTION_WAIT);
end;

///////////////////////////////////////////////////////////////////////////////
//
// Function: UninstallInliteKeyDrivers
//
//  Purpose: This function uninstalls the drivers for the Inlite Key
///////////////////////////////////////////////////////////////////////////////
function UninstallInliteKeyDrivers()
	string szProgram;
begin       
  	SdShowMsg("Setup is Uninstalling Inlite drivers.", TRUE);
   
   	szProgram = SUPPORTDIR ^ "\\CBUSetup.exe";
	LaunchAppAndWait(szProgram, "/Q /U /CRYPTOKEN", LAAW_OPTION_WAIT); 
	
	szProgram = SUPPORTDIR ^ "\\UninstallInlite.bat";
	LaunchAppAndWait("cmd", "/c "+ szProgram,
		LAAW_OPTION_WAIT | LAAW_OPTION_HIDDEN | LAAW_OPTION_SHOW_HOURGLASS );

 	SdShowMsg("Setup is Uninstalling Inlite drivers.", FALSE);
end;

///////////////////////////////////////////////////////////////////////////////
//
// Function: InstallCrystalReports
//
//  Purpose: This function installs the Crystal Reports Runtime.
//	Returns 0 if no errors
///////////////////////////////////////////////////////////////////////////////
function InstallCrystalReports()
STRING szProgram, szCmdLine;
int nResult, svServiceState;
begin
	SdShowMsg("Setup is Installing Crystal Reports 12 Runtime", TRUE);
	
	// Install Crystal Reports
	szProgram = "msiexec";
	szCmdLine = " /qn /i " + SUPPORTDIR ^ "\\CRRuntime_12_1_mlb.msi";
	LaunchAppAndWait(szProgram, szCmdLine, LAAW_OPTION_WAIT | LAAW_OPTION_SHOW_HOURGLASS); 
	
	SdShowMsg("Setup is Installing Crystal Reports 12 Runtime", FALSE);
end;

///////////////////////////////////////////////////////////////////////////////
//
// Function: CloseApps
//
//  Purpose: To close running apps before installing or uninstalling
///////////////////////////////////////////////////////////////////////////////
function CloseApps()
	string strExtractTRPPath;
begin
	strExtractTRPPath = PROGRAMFILES ^ EXTRACT_DIR ^ "ExtractTRP2.exe";
	LaunchAppAndWait(strExtractTRPPath , "/exit", LAAW_OPTION_WAIT | LAAW_OPTION_HIDDEN | LAAW_OPTION_SHOW_HOURGLASS );
end;


///////////////////////////////////////////////////////////////////////////////
//
// Function: UninstallCrystalReports
//
//  Purpose: This function uninstalls the Crystal Reports Runtime.
//	Returns 0 if no errors
///////////////////////////////////////////////////////////////////////////////
function UninstallCrystalReports12()
STRING szProgram, szCmdLine;
int nResult, svServiceState;
begin
		// Ask user if they wish to unintall CrystalReports
	nResult = MessageBox("Do you wish to uninstall Crystal Reports 12 Runtime?" + 
		"\r\nOther installed applications may use this and uninstalling " + 
		" will cause those applications to work incorrectly." , MB_YESNO );

	if (nResult = IDYES) then
	    UninstallApplication(CRYSTAL_REPORTS_GUID,"", LAAW_OPTION_WAIT  | LAAW_OPTION_SHOW_HOURGLASS);
  	endif;
end;


///////////////////////////////////////////////////////////////////////////////
//
// Function: InstallSQLCE
//
//  Purpose: This function installs the SQL Compact Edition if it is not installed
//	Returns 0 if no errors
///////////////////////////////////////////////////////////////////////////////
function InstallSQLCE()
STRING szProgram, szCmdLine, strUninstallKey;
int nResult, svServiceState;
begin
	// Change the root for the registry
	RegDBSetDefaultRoot(HKEY_LOCAL_MACHINE);
	if ( RegDBKeyExist ( REG_PATH_SQL_COMPACT_EDITION ) < 0) then
		SdShowMsg("Setup is Installing SQL Compact Edition", TRUE);

		// Install SQL Compact Edition
		// 32bit needs to be installed even on 64 bit
		szProgram = "msiexec";
		szCmdLine = " /i " + SUPPORTDIR ^ "\\SSCERuntime-ENU-x86.msi" + " /qb";
		LaunchAppAndWait(szProgram, szCmdLine, LAAW_OPTION_WAIT); 

		// If 64 bit OS install 64 version of SSCE	
		if ( SYSINFO.bIsWow64 ) then
			szCmdLine = " /i " + SUPPORTDIR ^ "\\SSCERuntime-ENU-x64.msi" + " /qb";
			LaunchAppAndWait(szProgram, szCmdLine, LAAW_OPTION_WAIT); 
		endif;
		SdShowMsg("Setup is Installing SQL Compact Edition", FALSE);
		//if ( RegDBKeyExist ( REG_PATH_SQL_COMPACT_EDITION ) < 0  ) then
		//	MessageBox("SQL Compact Edition was not successfully Installed, installation will be aborted", SEVERE);
		//	gbInstalled = FALSE;
		//	abort;
		//endif;
	endif;
end;

            
///////////////////////////////////////////////////////////////////////////////
//
//  FUNCTION:   DetectDotNET_40
//
//  EVENT:      Aborts with message if .NET 3.5 is not installed
//
///////////////////////////////////////////////////////////////////////////////
function DetectDotNET_40()
STRING szProgram, svValue;
int nResult, svServiceState;       
NUMBER nvType, nvSize;
begin
	// Change the root for the registry
	RegDBSetDefaultRoot(HKEY_LOCAL_MACHINE);
 
 	svValue = "";   
	RegDBGetKeyValueEx (REG_PATH_DOT_NET_40_INSTALL, "Install", nvType, svValue, nvSize);
	
	if ( svValue != "1") then
		gbInstalled  = FALSE;
		MessageBox("Microsoft .NET 4.0 is must be installed before running this install.", MB_OK);
		abort;
	endif; 
end;

///////////////////////////////////////////////////////////////////////////////
//
// Function: UninstallInstallSQLCE
//
//  Purpose: This function uninstalls the SQL Server Compact Edition.
//	Returns 0 if no errors
///////////////////////////////////////////////////////////////////////////////
function UninstallInstallSQLCE()
STRING szProgram, szCmdLine;
int nResult, svServiceState;
begin  
	if ( RegDBKeyExist ( REG_PATH_SQL_COMPACT_EDITION ) = 0) then
		// Ask user if they wish to unintall SQL Server CE
		nResult = MessageBox("Do you wish to uninstall SQL Compact Edition?" + 
			"\r\nOther installed applications may use this and uninstalling " + 
			" will cause those applications to work incorrectly." , MB_YESNO );

		if (nResult = IDYES) then
			SdShowMsg("Setup is Uninstalling SQL Compact Edition", TRUE);
	    	UninstallApplication(REG_PATH_SQL_COMPACT_EDITION,"", LAAW_OPTION_WAIT  | LAAW_OPTION_SHOW_HOURGLASS);
	    	UninstallApplication(SQL_CE_DATABASE_VIEWER_AND_EDITOR_CODE,"", LAAW_OPTION_WAIT  | LAAW_OPTION_SHOW_HOURGLASS);
			SdShowMsg("Setup is Installing SQL Compact Edition ", FALSE);
	  	endif;
	endif;
end;

///////////////////////////////////////////////////////////////////////////////
//
//  FUNCTION:   InstallDotNetService
//
//  EVENT:      Installs a Dot Net service 
//
///////////////////////////////////////////////////////////////////////////////
function InstallDotNetService(strServiceName)
begin
	LaunchAppAndWait( INSTALL_UTIL_PATH , strServiceName, LAAW_OPTION_WAIT | LAAW_OPTION_HIDDEN | LAAW_OPTION_SHOW_HOURGLASS );
end;
     
     
///////////////////////////////////////////////////////////////////////////////
//
//  FUNCTION:   UninstallDotNetService
//
//  EVENT:      Uninstalls a Dot Net service 
//
///////////////////////////////////////////////////////////////////////////////
function UninstallDotNetService(strServiceName)
STRING szCmdLine;
begin
	szCmdLine = "/u " + strServiceName;            
	LaunchAppAndWait( INSTALL_UTIL_PATH , szCmdLine, LAAW_OPTION_WAIT | LAAW_OPTION_HIDDEN | LAAW_OPTION_SHOW_HOURGLASS );
end;
     
     
///////////////////////////////////////////////////////////////////////////////
//
// Function: AreOtherExtractProductsInstalled
//
//  Purpose: This function checks for other Extract Systems products
//			 returns TRUE if there is another product installed
// NOTE: Does not check to see if LM is installed
///////////////////////////////////////////////////////////////////////////////
function BOOL AreOtherExtractProductsInstalled()
BOOL bReturnValue;
STRING strVersion;
begin
    bReturnValue = FALSE;
	RegDBSetDefaultRoot(HKEY_LOCAL_MACHINE);
 	
    // Check each of the expected version against any installed versions
    if ((PRODUCT_GUID!= IDSHIELD_PRODUCT_CODE) && 
    		CheckProductKeyInstalled(@FlexIndexVersion, IDSHIELD_PRODUCT_CODE, strVersion, FALSE)) then
    	bReturnValue = TRUE;
	elseif ((PRODUCT_GUID != FLEX_INDEX_PRODUCT_CODE) && 
    		CheckProductKeyInstalled(@FlexIndexVersion, FLEX_INDEX_PRODUCT_CODE, strVersion, FALSE)) then
    	bReturnValue = TRUE;
	elseif ((PRODUCT_GUID != LASERFICHE_PRODUCT_CODE) && 
    		CheckProductKeyInstalled(@LaserficheVersion, LASERFICHE_PRODUCT_CODE, strVersion, FALSE)) then 
    	bReturnValue = TRUE;
	elseif ((PRODUCT_GUID != IDSHIELDOFFICE_PRODUCT_CODE) && 
    		CheckProductKeyInstalled(@IDShieldOfficeVersion, IDSHIELDOFFICE_PRODUCT_CODE, strVersion, FALSE)) then 
    	bReturnValue = TRUE;
   	elseif ((PRODUCT_GUID != LABDE_PRODUCT_CODE) && 
    		CheckProductKeyInstalled(@LabDEVersion, LABDE_PRODUCT_CODE, strVersion, FALSE)) then 
    	bReturnValue = TRUE;
	endif;
	
	return bReturnValue;    	
end;


///////////////////////////////////////////////////////////////////////////////
//
// Function: InstallOfficeRedist
//
//  Purpose: This function installs the Office redist.
//	Returns 0 if no errors
///////////////////////////////////////////////////////////////////////////////
function InstallOfficeRedist()
STRING szProgram, szCmdLine;
int nResult, svServiceState;
begin
	SdShowMsg("Setup is Installing Microsoft Office Redistributable", TRUE);
	
	// Install Microsoft Office Redistributable
	szProgram = "msiexec";
	szCmdLine = " /qn /i " + SUPPORTDIR ^ "\\o2007pia.msi";
	LaunchAppAndWait(szProgram, szCmdLine, LAAW_OPTION_WAIT | LAAW_OPTION_SHOW_HOURGLASS); 
	
	SdShowMsg("Setup is Installing Microsoft Office Redistributable", FALSE);
end;
